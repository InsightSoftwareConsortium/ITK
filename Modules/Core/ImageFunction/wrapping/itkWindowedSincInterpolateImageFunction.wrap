set(WRAPPER_AUTO_INCLUDE_HEADERS OFF)
itk_wrap_include("itkWindowedSincInterpolateImageFunction.h")
itk_wrap_include("itkPhasedArray3DSpecialCoordinatesImage.h")

set(window_functions
    "Hamming"
    "Cosine"
    "Welch"
    "Lanczos")
set(radii 2 3)
foreach(function ${window_functions})
  itk_wrap_class("itk::Function::${function}WindowFunction")
  foreach(r ${radii})
    itk_wrap_template("${r}" "${r}")
  endforeach()
  itk_end_wrap_class()
endforeach()

itk_wrap_class("itk::WindowedSincInterpolateImageFunction" POINTER)
foreach(d ${ITK_WRAP_IMAGE_DIMS})
  foreach(t ${WRAP_ITK_SCALAR})
    foreach(r ${radii}) # radius
      foreach(function ${window_functions})
        itk_wrap_template("${ITKM_I${t}${d}}${r}${function}"
                          "${ITKT_I${t}${d}}, ${r}, itk::Function::${function}WindowFunction< ${r} >")
      endforeach()
    endforeach()
  endforeach()
endforeach()

set(PA3DSCI_types "F")
if(ITK_WRAP_unsigned_char)
  list(APPEND PA3DSCI_types "UC")
endif()
foreach(t ${PA3DSCI_types})
  foreach(r ${radii}) # radius
    foreach(function ${window_functions})
      itk_wrap_template(
        "PA3DSCI${ITKM_${t}}${r}${function}"
        "itk::PhasedArray3DSpecialCoordinatesImage< ${ITKT_${t}} >, ${r}, itk::Function::${function}WindowFunction< ${r} >"
      )
    endforeach()
  endforeach()
endforeach()
itk_end_wrap_class()
