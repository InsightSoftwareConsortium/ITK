itk_wrap_include("itkPhasedArray3DSpecialCoordinatesImage.h")
itk_wrap_class("itk::InPlaceImageFilter" POINTER)
# VectorImage <-> scalar
unique(to_types "UC;${WRAP_ITK_SCALAR}")
foreach(d ${ITK_WRAP_IMAGE_DIMS})
  foreach(t ${to_types})
    itk_wrap_template("${ITKM_VI${t}${d}}${ITKM_I${t}${d}}" "${ITKT_VI${t}${d}},${ITKT_I${t}${d}}")
    itk_wrap_template("${ITKM_I${t}${d}}${ITKM_VI${t}${d}}" "${ITKT_I${t}${d}},${ITKT_VI${t}${d}}")
  endforeach()
endforeach()

# *Vector <-> scalar
itk_wrap_image_filter_combinations("${WRAP_ITK_VECTOR}" "${WRAP_ITK_SCALAR}")
itk_wrap_image_filter_combinations("${WRAP_ITK_SCALAR}" "${WRAP_ITK_VECTOR}")

# RGB -> Scalar
foreach(type ${WRAP_ITK_SCALAR})
  foreach(rgbtype ${WRAP_ITK_RGB})
    itk_wrap_image_filter_combinations("${rgbtype}" "${type}")
  endforeach()
endforeach()

# complex types
itk_wrap_image_filter_combinations("${WRAP_ITK_COMPLEX_REAL}" "${WRAP_ITK_COMPLEX_REAL}")

# complex <-> scalar
itk_wrap_image_filter_combinations("${WRAP_ITK_SCALAR}" "${WRAP_ITK_COMPLEX_REAL}")
itk_wrap_image_filter_combinations("${WRAP_ITK_COMPLEX_REAL}" "${WRAP_ITK_SCALAR}")

# Wrap dim=3 -> dim=2, dim=3 -> dim=2, etc.
foreach(d ${ITK_WRAP_IMAGE_DIMS})
  foreach(d2 ${ITK_WRAP_IMAGE_DIMS})
    if(NOT
       "${d}"
       EQUAL
       "${d2}") # this was already taken care of elsewhere
      foreach(t ${WRAP_ITK_SCALAR} ${WRAP_ITK_RGB} ${WRAP_ITK_COMPLEX_REAL})
        itk_wrap_template("${ITKM_I${t}${d}}${ITKM_I${t}${d2}}" "${ITKT_I${t}${d}},${ITKT_I${t}${d2}}")
      endforeach()
    endif()
  endforeach()
endforeach()

# PhasedArray3DSpecialCoordinatesImage
foreach(t ${WRAP_ITK_SCALAR})
  foreach(ut ${WRAP_ITK_INT})
    itk_wrap_template("I${ITKM_${t}}3PA3DSCI${ITKM_${ut}}"
                      "itk::Image< ${ITKT_${t}}, 3 >, itk::PhasedArray3DSpecialCoordinatesImage< ${ITKT_${ut}} >")
  endforeach()
endforeach()

itk_end_wrap_class()
