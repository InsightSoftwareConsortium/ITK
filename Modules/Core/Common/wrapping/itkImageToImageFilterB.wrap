itk_wrap_class("itk::ImageToImageFilter" POINTER)

  # scalar <-> RGB
  UNIQUE(stypes "${ITKM_IT};UL;${WRAP_ITK_SCALAR}")
  itk_wrap_image_filter_combinations("${stypes}" "${WRAP_ITK_RGB}")
  itk_wrap_image_filter_combinations("${WRAP_ITK_RGB}" "${stypes}")

  # scalar <-> RGBA
  UNIQUE(stypes "${ITKM_IT};UL;${WRAP_ITK_SCALAR}")
  itk_wrap_image_filter_combinations("${stypes}" "${WRAP_ITK_RGBA}")
  itk_wrap_image_filter_combinations("${WRAP_ITK_RGBA}" "${stypes}")

  # VectorImage <-> scalar
  UNIQUE(to_types "UC;${WRAP_ITK_SCALAR}")
    foreach(d ${ITK_WRAP_IMAGE_DIMS})
      foreach(t ${to_types})
        foreach(t2 ${to_types})
          itk_wrap_template("${ITKM_VI${t}${d}}${ITKM_I${t2}${d}}" "${ITKT_VI${t}${d}},${ITKT_I${t2}${d}}")
          itk_wrap_template("${ITKM_I${t2}${d}}${ITKM_VI${t}${d}}" "${ITKT_I${t2}${d}},${ITKT_VI${t}${d}}")
        endforeach()
      endforeach()
    endforeach()
  # *Vector <-> scalar
  itk_wrap_image_filter_combinations("${WRAP_ITK_VECTOR}" "${WRAP_ITK_SCALAR}")
  itk_wrap_image_filter_combinations("${WRAP_ITK_SCALAR}" "${WRAP_ITK_VECTOR}")

  # complex types
  itk_wrap_image_filter_combinations("${WRAP_ITK_COMPLEX_REAL}" "${WRAP_ITK_COMPLEX_REAL}")

  # complex <-> scalar
  itk_wrap_image_filter_combinations("${WRAP_ITK_SCALAR}" "${WRAP_ITK_COMPLEX_REAL}")
  itk_wrap_image_filter_combinations("${WRAP_ITK_COMPLEX_REAL}" "${WRAP_ITK_SCALAR}")

  UNIQUE(real_types "${WRAP_ITK_REAL};D")
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t2 ${real_types})
      foreach(t ${WRAP_ITK_SCALAR})
        # scalar <-> SymmetricSecondRankTensor
        itk_wrap_template("${ITKM_I${t}${d}}${ITKM_ISSRT${ITKM_${t2}}${d}${d}}" "${ITKT_I${t}${d}}, ${ITKT_ISSRT${ITKM_${t2}}${d}${d}}")
        itk_wrap_template("${ITKM_ISSRT${ITKM_${t2}}${d}${d}}${ITKM_I${t}${d}}" "${ITKT_ISSRT${ITKM_${t2}}${d}${d}}, ${ITKT_I${t}${d}}")
      endforeach()
      # SymmetricSecondRankTensor -> SymmetricSecondRankTensor
      foreach(t3 ${real_types})
        itk_wrap_template("${ITKM_ISSRT${ITKM_${t2}}${d}${d}}${ITKM_ISSRT${ITKM_${t3}}${d}${d}}"
                          "${ITKT_ISSRT${ITKM_${t2}}${d}${d}}, ${ITKT_ISSRT${ITKM_${t3}}${d}${d}}")
      endforeach()
    endforeach()
  endforeach()

  # Wrap dim=3 -> dim=2, dim=3 -> dim=2, etc.
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(d2 ${ITK_WRAP_IMAGE_DIMS})
      if(NOT "${d}" EQUAL "${d2}") # this was already taken care of elsewhere
        foreach(t ${WRAP_ITK_SCALAR} ${WRAP_ITK_RGB} ${WRAP_ITK_COMPLEX_REAL})
          itk_wrap_template("${ITKM_I${t}${d}}${ITKM_I${t}${d2}}"
                        "${ITKT_I${t}${d}},${ITKT_I${t}${d2}}")
        endforeach()
      endif()
    endforeach()
  endforeach()

  list(FIND WRAP_ITK_COV_VECTOR_REAL "CVD" index)
  if(index EQUAL -1)
    # Real <-> CovariantVector
    itk_wrap_include("itkCovariantVector.h")
    foreach(d ${ITK_WRAP_IMAGE_DIMS})
      itk_wrap_template("I${ITKM_F}${d}ICV${ITKM_D}${d}${d}"
                        "itk::Image<${ITKT_F}, ${d}>, itk::Image<itk::CovariantVector<${ITKT_D}, ${d}>, ${d}>")
      itk_wrap_template("I${ITKM_D}${d}ICV${ITKM_D}${d}${d}"
                        "itk::Image<${ITKT_D}, ${d}>, itk::Image<itk::CovariantVector<${ITKT_D}, ${d}>, ${d}>")
    endforeach()
  endif()

itk_end_wrap_class()
