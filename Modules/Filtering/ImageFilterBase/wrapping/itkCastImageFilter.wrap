itk_wrap_class("itk::CastImageFilter" POINTER_WITH_SUPERCLASS)
  # Create cast filters between all scalar types. Also force that cast-to-uchar
  # filters are created for all scalar types, and unsigned long for
  # segmentation output casting.
  UNIQUE(types "${WRAP_ITK_SCALAR};UC;UL")
  itk_wrap_image_filter_combinations("${types}" "${types}")

  # vector <-> vector
  itk_wrap_image_filter_combinations("${WRAP_ITK_VECTOR}" "${WRAP_ITK_VECTOR}")

  # VectorImage <-> vector
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    if(ITK_WRAP_vector_float)
      itk_wrap_template("${ITKM_IVF${d}${d}}${ITKM_VIF${d}}" "${ITKT_IVF${d}${d}},${ITKT_VIF${d}}")
    endif()
    if(ITK_WRAP_vector_double)
      itk_wrap_template("${ITKM_IVD${d}${d}}${ITKM_VID${d}}" "${ITKT_IVD${d}${d}},${ITKT_VID${d}}")
    endif()
  endforeach()

  # RGB <-> RGB
  UNIQUE(rgb "RGBUC;${WRAP_ITK_RGB}")
  itk_wrap_image_filter_combinations("${rgb}" "${rgb}")

  # vector <-> RGB
  # itk_wrap_image_filter_combinations("${WRAP_ITK_VECTOR}" "${WRAP_ITK_RGB}" 3)
  # itk_wrap_image_filter_combinations("${WRAP_ITK_RGB}" "${WRAP_ITK_VECTOR}" 3)

  # Enable casting 64 bit unsigned int to smaller integer types
  if(ITK_WRAP_unsigned_long OR NOT ${ITKM_IT} STREQUAL "ULL")
    itk_wrap_image_filter_combinations("ULL" "${WRAP_ITK_INT}")
  endif()
itk_end_wrap_class()
